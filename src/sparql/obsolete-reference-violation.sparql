prefix rdf:   <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix oio: <http://www.geneontology.org/formats/oboInOwl#>
prefix owl: <http://www.w3.org/2002/07/owl#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT DISTINCT ?obsolete
WHERE {
  ?obsolete owl:deprecated "true"^^xsd:boolean .  
  { # Check for obsolete term used as subclass
    ?obsolete rdfs:subClassOf ?ref . 
    FILTER(?ref != owl:Thing) # This filter is needed as long as this check is being run against reasoned.owl
  }
  UNION 
  { # Check for obsolete term used as superclass
    ?ref rdfs:subClassOf ?obsolete . 
  }
  UNION 
  { # Check for obsolete term used as subject of equivalence
    ?obsolete owl:equivalentClass ?ref . 
  }
  UNION 
  { # Check for obsolete term used as object of equivalence
    ?ref owl:equivalentClass ?obsolete . 
  }
  UNION
  { # Check for obsolete term used as existential filler
    ?ref owl:someValuesFrom ?obsolete . 
  }
  UNION
  { # Check for obsolete term used as universal filler
    ?ref owl:allValuesFrom ?obsolete . 
  }
  UNION
  { # Check for obsolete term used in intersection or union
    ?ref rdf:first ?obsolete . 
  }
  UNION
  { # Check for obsolete term used in slim
    ?obsolete oio:inSubset ?subset .
  }
}
